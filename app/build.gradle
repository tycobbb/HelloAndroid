apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'

buildscript {
    ext.kotlin_version = '1.0.0-beta-1038'
    ext.dagger_version = '2.1-SNAPSHOT'

    repositories {
        mavenCentral()
        maven {
            url 'http://oss.sonatype.org/content/repositories/snapshots'
        }
    }

    dependencies {
        // plugin needs to use snapshot to support kapt
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
        classpath "org.jetbrains.kotlin:kotlin-android-extensions:$kotlin_version"
    }
}

def loadConfig(buildConfigField, filename) {
    def props = new Properties()
    props.load(new FileInputStream(file(filename)))

    buildConfigField "String", "RIOT_API_KEY", props.getProperty("riot_api_key")
}

android {

    compileSdkVersion 23
    buildToolsVersion "23.0.1"

    defaultConfig {
        applicationId "dev.wizrad.helloandroid"
        versionCode 1
        versionName "1.0"
        minSdkVersion 22
        targetSdkVersion 23
    }

    buildTypes {
        loadConfig(debug.&buildConfigField, "../debug.properties")
        loadConfig(release.&buildConfigField, "../release.properties")

        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    sourceSets {
        main.java.srcDirs += 'src/main/kotlin'
        test.java.srcDirs += 'src/test/kotlin'
        androidTest.java.srcDirs += 'src/androidTest/kotlin'
    }
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])

    // kotlin
    compile "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"

    // rx
    compile 'io.reactivex:rxjava:1.0.14'
    compile 'io.reactivex:rxandroid:1.0.1'
    compile 'com.jakewharton.rxbinding:rxbinding:0.2.0'

    // dagger2
    kapt "com.google.dagger:dagger-compiler:$dagger_version"
    compile "com.google.dagger:dagger:$dagger_version"
    provided 'org.glassfish:javax.annotation:10.0-b28'

    // retrofit
    compile 'com.squareup.retrofit:retrofit:2.0.0-beta1'
    compile 'com.squareup.retrofit:adapter-rxjava:2.0.0-beta1'
    compile 'com.squareup.retrofit:converter-gson:2.0.0-beta1'
    compile 'com.google.code.gson:gson:2.3.1'

    // android support
    compile 'com.android.support:appcompat-v7:23.1.0'
    compile 'com.android.support:design:23.1.0'

    // butterknife
    compile 'com.jakewharton:kotterknife:0.1.0-SNAPSHOT'

    // spek
    testCompile "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
    testCompile "org.jetbrains.spek:spek:0.1.180"
}

repositories {
    mavenCentral()
    maven { url "http://repository.jetbrains.com/all" }
    maven { url "https://oss.sonatype.org/content/repositories/snapshots/" }
}

kapt {
    generateStubs = true
}
